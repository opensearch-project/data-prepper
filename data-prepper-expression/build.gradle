/*
 * Copyright OpenSearch Contributors
 * SPDX-License-Identifier: Apache-2.0
 */

plugins {
    id 'antlr'
    id 'idea'
}

ext {
    antlrGeneratedPackageDirectory = "org/opensearch/dataprepper/expression/antlr/"
}

dependencies {
    antlr('org.antlr:antlr4:4.10.1') {
        exclude group: 'org.glassfish', module: 'javax.json'
    }
    implementation project(':data-prepper-api')
    implementation 'javax.inject:javax.inject:1'
    implementation('org.springframework:spring-core:6.0.2') {
        exclude group: 'commons-logging', module: 'commons-logging'
    }
    implementation('org.springframework:spring-context:5.3.23') {
        exclude group: 'commons-logging', module: 'commons-logging'
    }
    implementation platform('org.apache.logging.log4j:log4j-bom:2.19.0')
    implementation 'org.apache.logging.log4j:log4j-core'
    implementation 'org.apache.logging.log4j:log4j-slf4j-impl'
    testImplementation 'org.springframework:spring-test:5.3.23'
    testImplementation 'com.fasterxml.jackson.core:jackson-databind'
}

generateGrammarSource {
    outputDirectory = new File("build/generated-src/antlr/main/${antlrGeneratedPackageDirectory}")
}

jacocoTestCoverageVerification {
    dependsOn jacocoTestReport
    violationRules {
        rule { //in addition to core projects rule - this one checks for 100% code coverage for this project
            limit {
                minimum = 1.0 //keep this at 100%
            }
        }
    }
    afterEvaluate {
        classDirectories.setFrom(files(classDirectories.files.collect {
            fileTree(dir: it, exclude: ["${antlrGeneratedPackageDirectory}/**"])
        }))
    }
}
