/*
 * Copyright OpenSearch Contributors
 * SPDX-License-Identifier: Apache-2.0
 */

plugins {
    id 'java'
}

sourceSets {
    integrationTest {
        java {
            compileClasspath += main.output + test.output
            runtimeClasspath += main.output + test.output
            srcDir file('src/integrationTest/java')
        }
    }
}

test {
    useJUnitPlatform()
}

configurations {
    integrationTestImplementation.extendsFrom testImplementation
    integrationTestRuntime.extendsFrom testRuntime
}

dependencies {
    implementation project(':data-prepper-api')
    implementation project(':data-prepper-plugins:common')
    implementation project(path: ':data-prepper-plugins:buffer-common')
    implementation project(path: ':data-prepper-plugins:aws-plugin-api')
    implementation 'com.fasterxml.jackson.core:jackson-core'
    implementation 'io.micrometer:micrometer-core'
    implementation 'software.amazon.kinesis:amazon-kinesis-client:2.6.0'
    compileOnly 'org.projectlombok:lombok:1.18.20'
    annotationProcessor 'org.projectlombok:lombok:1.18.20'

    testImplementation 'com.fasterxml.jackson.dataformat:jackson-dataformat-yaml'
    testImplementation 'com.fasterxml.jackson.datatype:jackson-datatype-jsr310'
    testImplementation project(':data-prepper-test-common')
    testImplementation project(':data-prepper-test-event')
    testImplementation project(':data-prepper-core')
    testImplementation project(':data-prepper-event')
    testImplementation project(':data-prepper-plugin-framework')
    testImplementation project(':data-prepper-pipeline-parser')
    testImplementation 'com.fasterxml.jackson.dataformat:jackson-dataformat-yaml'
    testImplementation project(':data-prepper-plugins:parse-json-processor')
    testImplementation project(':data-prepper-plugins:newline-codecs')
}

task integrationTest(type: Test) {
    group = 'verification'
    testClassesDirs = sourceSets.integrationTest.output.classesDirs
    useJUnitPlatform()

    classpath = sourceSets.integrationTest.runtimeClasspath
    systemProperty 'tests.kinesis.source.aws.region', System.getProperty('tests.kinesis.source.aws.region')

    filter {
        includeTestsMatching '*IT'
    }
}

jacocoTestCoverageVerification {
    dependsOn jacocoTestReport
    violationRules {
        rule { //in addition to core projects rule
            limit {
                minimum = 1.0
            }
        }
    }
}