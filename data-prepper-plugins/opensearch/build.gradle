/*
 * Copyright OpenSearch Contributors
 * SPDX-License-Identifier: Apache-2.0
 */

buildscript {
    ext {
        opensearch_version = System.getProperty('opensearch.version', "${versionMap.opensearchVersion}")
        opensearch_group = 'org.opensearch'
        distribution = 'oss-zip'
    }

    repositories {
        mavenCentral()
        gradlePluginPortal()
    }

    dependencies {
        classpath "${opensearch_group}.gradle:build-tools:${opensearch_version}"
        constraints {
            classpath('com.netflix.nebula:nebula-core') {
                version {
                    require '3.0.1'
                }
                because 'Nebula 3.0.0 is not in Maven Central and any dependency on it will attempt to use JCenter.'
            }
        }
    }
}

plugins {
    id 'java'
}

repositories {
    mavenCentral()
}

apply plugin: 'opensearch.testclusters'
apply plugin: 'opensearch.build'
apply plugin: 'opensearch.rest-test'

// To pass OpenSearch plugin check
ext {
    licenseFile = rootProject.file('LICENSE')
    noticeFile = rootProject.file('NOTICE')
}

dependencies {
    api project(':data-prepper-api')
    testImplementation project(':data-prepper-api').sourceSets.test.output
    api project(':data-prepper-plugins:common')
    implementation "org.opensearch.client:opensearch-rest-high-level-client:${opensearch_version}"
    implementation 'com.fasterxml.jackson.core:jackson-databind'
    implementation 'com.fasterxml.jackson.dataformat:jackson-dataformat-yaml'
    implementation 'javax.ws.rs:javax.ws.rs-api:2.1.1'
    implementation 'software.amazon.awssdk:auth:2.17.15'
    implementation 'software.amazon.awssdk:http-client-spi:2.17.15'
    implementation 'software.amazon.awssdk:sdk-core:2.17.15'
    implementation 'software.amazon.awssdk:aws-core:2.17.15'
    implementation 'software.amazon.awssdk:regions:2.17.15'
    implementation 'software.amazon.awssdk:utils:2.17.15'
    implementation 'software.amazon.awssdk:sts:2.17.15'
    implementation 'software.amazon.awssdk:url-connection-client:2.17.15'
    implementation 'software.amazon.awssdk:arns:2.17.15'
    implementation 'io.micrometer:micrometer-core:1.8.4'
    // The OpenSearch build-tools plugin appears to be preventing Gradle's platform
    // support from working correctly, so we have to specify the JUnit versions here.
    testImplementation "org.junit.jupiter:junit-jupiter:${versionMap.junitJupiter}"
    testImplementation "org.junit.vintage:junit-vintage-engine:${versionMap.junitJupiter}"
    testImplementation('junit:junit:4.13.2') {
        exclude group:'org.hamcrest' // workaround for jarHell
    }
    testImplementation "org.awaitility:awaitility:4.1.1"
    testImplementation "org.opensearch.test:framework:${opensearch_version}"
    testImplementation "commons-io:commons-io:2.11.0"
    testImplementation 'net.bytebuddy:byte-buddy:1.12.8'
    testImplementation 'net.bytebuddy:byte-buddy-agent:1.11.20'
}

// Workaround for Werror
compileJava.options.warnings = false

// Resolve dependency conflict between ES sink and main project
configurations.all {
    resolutionStrategy {
        force 'com.amazonaws:aws-java-sdk-core:1.12.159'
        force 'com.fasterxml.jackson.dataformat:jackson-dataformat-yaml:2.13.2'
        force 'com.fasterxml.jackson.core:jackson-annotations:2.13.2'
        force 'com.fasterxml.jackson.core:jackson-databind:2.13.2'
        force 'com.fasterxml.jackson.core:jackson-core:2.13.2'
        force 'com.fasterxml.jackson:jackson-bom:2.13.2'
        force 'com.fasterxml.jackson.dataformat:jackson-dataformat-cbor:2.13.2'
        force 'com.fasterxml.jackson.dataformat:jackson-dataformat-smile:2.13.2'
        force 'commons-codec:commons-codec:1.15'
        force 'commons-logging:commons-logging:1.2'
        force 'org.apache.httpcomponents:httpclient:4.5.13'
        force 'org.apache.httpcomponents:httpcore:4.4.15'
        force "org.hdrhistogram:HdrHistogram:2.1.12"
        force 'joda-time:joda-time:2.10.13'
        force 'org.yaml:snakeyaml:1.30'
        force 'com.google.guava:guava:31.0.1-jre'
        force 'junit:junit:4.13.2'
        force "org.slf4j:slf4j-api:1.7.36"
        force 'org.apache.logging.log4j:log4j-api:2.17.1'
        force 'org.apache.logging.log4j:log4j-core:2.17.1'
        force 'commons-beanutils:commons-beanutils:1.9.4'
    }
    // The OpenSearch plugins appear to provide their own version of Mockito
    // which is causing problems, so we exclude it.
    exclude (group: 'org.elasticsearch', module: "securemock");
}

test {
    // Workaround: opensearch plugin is not compatible with JUnitPlatform
    useJUnit()

    if (System.getProperty("os.host") == null) {
        exclude '**/OpenSearchTests.class'
    }
    systemProperty "os.host", System.getProperty("os.host")
    if (System.getProperty("os.user") != null) {
        systemProperty "os.user", System.getProperty("os.user")
        systemProperty "os.password", System.getProperty("os.password")
    }
}

testClusters.integTest {
    testDistribution = "INTEG_TEST"
}

integTest {
    systemProperty 'tests.security.manager', 'false'

    systemProperty "os", System.getProperty("os")
    systemProperty "user", System.getProperty("user")
    systemProperty "password", System.getProperty("password")
}

jacocoTestReport {
    dependsOn test
    reports {
        xml.enabled true
    }
    afterEvaluate {
        classDirectories.from = files(classDirectories.files.collect {
            fileTree(dir: it, exclude: [
                    // TODO: find a way to include the classes covered by OpenSearch test framework
                    'com/amazon/dataprepper/plugins/sink/opensearch/OpenSearchSink.class',
                    'com/amazon/dataprepper/plugins/sink/opensearch/IndexStateManagement.class'
            ])
        })
    }
}

jacocoTestCoverageVerification {
    dependsOn jacocoTestReport
    violationRules {
        rule {
            element = 'CLASS'
            excludes = [
                    // TODO: find a way to include the classes covered by OpenSearch test framework
                    'com.amazon.dataprepper.plugins.sink.opensearch.OpenSearchSink',
                    'com.amazon.dataprepper.plugins.sink.opensearch.IndexStateManagement'
            ]
            limit {
                minimum = 0.90
            }
        }
    }
}

checkstyleMain.ignoreFailures = true
checkstyleTest.ignoreFailures = true
forbiddenApis.ignoreFailures = true
testingConventions.enabled = false
licenseHeaders.enabled = false
dependencyLicenses.enabled = false
thirdPartyAudit.enabled = false
loggerUsageCheck.enabled = false
validateNebulaPom.enabled = false
