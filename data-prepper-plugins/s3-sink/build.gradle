/*
 * Copyright OpenSearch Contributors
 * SPDX-License-Identifier: Apache-2.0
 */

dependencies {
    implementation project(':data-prepper-api')
    implementation project(path: ':data-prepper-plugins:common')
    implementation project(':data-prepper-plugins:aws-plugin-api')
    implementation 'io.micrometer:micrometer-core'
    implementation 'com.fasterxml.jackson.core:jackson-core'
    implementation 'com.fasterxml.jackson.core:jackson-databind'
    implementation libs.commons.compress
    implementation 'joda-time:joda-time:2.12.5'
    implementation 'org.hibernate.validator:hibernate-validator:8.0.1.Final'
    implementation 'com.fasterxml.jackson.dataformat:jackson-dataformat-csv'
    implementation 'software.amazon.awssdk:s3'
    implementation 'software.amazon.awssdk:sts'
    implementation 'org.jetbrains.kotlin:kotlin-stdlib:1.9.22'
    implementation project(':data-prepper-plugins:avro-codecs')
    implementation libs.avro.core
    implementation libs.hadoop.common
    implementation 'org.apache.parquet:parquet-avro:1.13.1'
    implementation 'software.amazon.awssdk:apache-client'
    implementation 'org.jetbrains.kotlin:kotlin-stdlib-common:1.9.22'
    implementation libs.commons.lang3
    testImplementation project(':data-prepper-test-common')
    testImplementation project(':data-prepper-plugins:parquet-codecs')
    testImplementation project(':data-prepper-plugins:parse-json-processor')
    testImplementation project(':data-prepper-plugins:csv-processor')
    testImplementation testLibs.slf4j.simple
    testImplementation 'software.amazon.awssdk:s3-transfer-manager'
    testImplementation 'software.amazon.awssdk.crt:aws-crt:0.29.11'

    constraints {
        implementation('com.nimbusds:nimbus-jose-jwt') {
            version {
                require '9.37.1'
            }
            because 'Fixes CVE-2021-31684 and CVE-2023-1370 by using a newer shaded version of json-smart.'
        }
    }
}

test {
    useJUnitPlatform()
}

sourceSets {
    integrationTest {
        java {
            compileClasspath += main.output + test.output
            runtimeClasspath += main.output + test.output
            srcDir file('src/integrationTest/java')
        }
        resources.srcDir file('src/integrationTest/resources')
    }
}

configurations {
    integrationTestImplementation.extendsFrom testImplementation
    integrationTestRuntime.extendsFrom testRuntime
}

task integrationTest(type: Test) {
    group = 'verification'
    testClassesDirs = sourceSets.integrationTest.output.classesDirs

    useJUnitPlatform()

    classpath = sourceSets.integrationTest.runtimeClasspath

    systemProperty 'log4j.configurationFile', 'src/test/resources/log4j2.properties'
    systemProperty 'tests.s3sink.bucket', System.getProperty('tests.s3sink.bucket')
    systemProperty 'tests.s3sink.region', System.getProperty('tests.s3sink.region')

    filter {
        includeTestsMatching '*IT'
    }
}
