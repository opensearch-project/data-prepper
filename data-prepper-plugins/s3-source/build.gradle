/*
 * Copyright OpenSearch Contributors
 * SPDX-License-Identifier: Apache-2.0
 */

plugins {
    id 'java'
}

repositories {
    mavenCentral()
}

dependencies {
    implementation project(':data-prepper-api')
    implementation project(':data-prepper-plugins:parquet-codecs')
    implementation project(':data-prepper-plugins:csv-processor')
    implementation project(':data-prepper-plugins:parse-json-processor')
    implementation project(':data-prepper-plugins:newline-codecs')
    implementation 'io.micrometer:micrometer-core'
    implementation 'software.amazon.awssdk:s3'
    implementation 'software.amazon.awssdk:sts'
    implementation 'software.amazon.awssdk:sqs'
    implementation 'com.fasterxml.jackson.core:jackson-core'
    implementation 'com.fasterxml.jackson.core:jackson-databind'
    implementation 'org.apache.commons:commons-compress:1.21'
    implementation 'joda-time:joda-time:2.11.1'
    implementation 'org.hibernate.validator:hibernate-validator:7.0.5.Final'
    implementation 'com.fasterxml.jackson.dataformat:jackson-dataformat-csv'
    testImplementation 'org.apache.commons:commons-lang3:3.12.0'
    testImplementation project(':data-prepper-test-common')
}

test {
    useJUnitPlatform()
}

sourceSets {
    integrationTest {
        java {
            compileClasspath += main.output + test.output
            runtimeClasspath += main.output + test.output
            srcDir file('src/integrationTest/java')
        }
        resources.srcDir file('src/integrationTest/resources')
    }
}

configurations {
    integrationTestImplementation.extendsFrom testImplementation
    integrationTestRuntime.extendsFrom testRuntime
}

task integrationTest(type: Test) {
    group = 'verification'
    testClassesDirs = sourceSets.integrationTest.output.classesDirs

    useJUnitPlatform()

    classpath = sourceSets.integrationTest.runtimeClasspath
    systemProperty 'tests.s3source.bucket', System.getProperty('tests.s3source.bucket')
    systemProperty 'tests.s3source.region', System.getProperty('tests.s3source.region')
    systemProperty 'tests.s3source.queue.url', System.getProperty('tests.s3source.queue.url')

    filter {
        includeTestsMatching '*IT'
    }
}

