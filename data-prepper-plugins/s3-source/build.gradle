/*
 * Copyright OpenSearch Contributors
 * SPDX-License-Identifier: Apache-2.0
 */

plugins {
    id 'java'
}

dependencies {
    implementation project(':data-prepper-api')
    implementation project(':data-prepper-plugins:buffer-common')
    implementation project(':data-prepper-plugins:common')
    implementation libs.armeria.core
    implementation 'io.micrometer:micrometer-core'
    implementation 'software.amazon.awssdk:s3'
    implementation 'software.amazon.awssdk:sts'
    implementation 'software.amazon.awssdk:sqs'
    implementation project(':data-prepper-plugins:aws-plugin-api')
    implementation 'software.amazon.awssdk:netty-nio-client'
    implementation 'com.fasterxml.jackson.core:jackson-core'
    implementation 'com.fasterxml.jackson.core:jackson-databind'
    implementation libs.commons.io
    implementation libs.commons.compress
    implementation 'joda-time:joda-time:2.12.7'
    implementation 'org.hibernate.validator:hibernate-validator:8.0.1.Final'
    implementation 'com.fasterxml.jackson.dataformat:jackson-dataformat-csv'
    implementation 'com.fasterxml.jackson.datatype:jackson-datatype-jsr310'
    implementation 'org.xerial.snappy:snappy-java:1.1.10.5'
    implementation 'org.apache.parquet:parquet-common:1.13.1'
    implementation 'dev.failsafe:failsafe:3.3.2'
    implementation 'org.apache.httpcomponents:httpcore:4.4.16'
    testImplementation libs.commons.lang3
    testImplementation 'org.wiremock:wiremock:3.3.1'
    testImplementation 'org.eclipse.jetty:jetty-bom:11.0.17'
    testImplementation 'com.fasterxml.jackson.dataformat:jackson-dataformat-yaml'
    testImplementation testLibs.junit.vintage
    testImplementation project(':data-prepper-test-common')
    testImplementation project(':data-prepper-plugins:csv-processor')
    testImplementation project(':data-prepper-plugins:parse-json-processor')
    testImplementation project(':data-prepper-plugins:newline-codecs')
    testImplementation project(':data-prepper-plugins:avro-codecs')
    testImplementation project(':data-prepper-plugins:in-memory-source-coordination-store')
    testImplementation project(':data-prepper-core')
    testImplementation project(':data-prepper-plugins:parquet-codecs')
    testImplementation libs.avro.core
    testImplementation testLibs.hadoop.common
    testImplementation 'org.apache.parquet:parquet-avro:1.13.1'
    testImplementation 'org.apache.parquet:parquet-column:1.13.1'
    testImplementation 'org.apache.parquet:parquet-common:1.13.1'
    testImplementation 'org.apache.parquet:parquet-hadoop:1.13.1'
}

test {
    useJUnitPlatform()
}

sourceSets {
    integrationTest {
        java {
            compileClasspath += main.output + test.output
            runtimeClasspath += main.output + test.output
            srcDir file('src/integrationTest/java')
        }
        resources.srcDir file('src/integrationTest/resources')
    }
}

configurations {
    integrationTestImplementation.extendsFrom testImplementation
    integrationTestRuntime.extendsFrom testRuntime
}

task integrationTest(type: Test) {
    group = 'verification'
    testClassesDirs = sourceSets.integrationTest.output.classesDirs

    useJUnitPlatform()

    classpath = sourceSets.integrationTest.runtimeClasspath
    systemProperty 'tests.s3source.bucket', System.getProperty('tests.s3source.bucket')
    systemProperty 'tests.s3source.region', System.getProperty('tests.s3source.region')
    systemProperty 'tests.s3source.queue.url', System.getProperty('tests.s3source.queue.url')

    filter {
        includeTestsMatching '*IT'
    }
}

