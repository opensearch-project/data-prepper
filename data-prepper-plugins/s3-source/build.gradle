/*
 * Copyright OpenSearch Contributors
 * SPDX-License-Identifier: Apache-2.0
 */

plugins {
    id 'java'
}

repositories {
    mavenCentral()
}

dependencies {
    implementation project(':data-prepper-api')
    implementation project(':data-prepper-plugins:buffer-common')
    implementation project(':data-prepper-plugins:common')
    implementation libs.armeria.core
    implementation 'io.micrometer:micrometer-core'
    implementation 'software.amazon.awssdk:s3'
    implementation 'software.amazon.awssdk:sts'
    implementation 'software.amazon.awssdk:sqs'
    implementation project(':data-prepper-plugins:aws-plugin-api')
    implementation 'software.amazon.awssdk:netty-nio-client'
    implementation 'com.fasterxml.jackson.core:jackson-core'
    implementation 'com.fasterxml.jackson.core:jackson-databind'
    implementation 'commons-io:commons-io:2.11.0'
    implementation 'org.apache.commons:commons-compress:1.21'
    implementation 'joda-time:joda-time:2.11.1'
    implementation 'org.hibernate.validator:hibernate-validator:7.0.5.Final'
    implementation 'com.fasterxml.jackson.dataformat:jackson-dataformat-csv'
    implementation 'com.fasterxml.jackson.datatype:jackson-datatype-jsr310:2.15.2'
    implementation 'org.xerial.snappy:snappy-java:1.1.10.1'
    implementation 'org.apache.parquet:parquet-common:1.12.3'
    testImplementation 'org.apache.commons:commons-lang3:3.12.0'
    testImplementation 'com.github.tomakehurst:wiremock:3.0.0-beta-8'
    testImplementation 'com.fasterxml.jackson.dataformat:jackson-dataformat-yaml'
    testImplementation testLibs.junit.vintage
    testImplementation project(':data-prepper-test-common')
    testImplementation project(':data-prepper-plugins:csv-processor')
    testImplementation project(':data-prepper-plugins:parse-json-processor')
    testImplementation project(':data-prepper-plugins:newline-codecs')
    testImplementation project(':data-prepper-plugins:avro-codecs')
    testImplementation project(':data-prepper-plugins:in-memory-source-coordination-store')
    testImplementation project(':data-prepper-core')
    testImplementation project(':data-prepper-plugins:parquet-codecs')
    testImplementation 'org.apache.avro:avro:1.11.0'
    testImplementation 'org.apache.hadoop:hadoop-common:3.3.5'
    testImplementation 'org.apache.hadoop:hadoop-hdfs-client:3.3.5'
    testImplementation 'org.apache.hadoop:hadoop-yarn-client:3.3.5'
    testImplementation 'org.apache.hadoop:hadoop-yarn-common:3.3.5'
    testImplementation 'org.apache.hadoop:hadoop-mapreduce-client-core:3.3.5'
    testImplementation 'org.apache.parquet:parquet-avro:1.12.3'
    testImplementation 'org.apache.parquet:parquet-column:1.12.3'
    testImplementation 'org.apache.parquet:parquet-common:1.12.3'
    testImplementation 'org.apache.parquet:parquet-hadoop:1.12.3'
    constraints {
        testImplementation('org.eclipse.jetty:jetty-bom') {
            version {
                require '11.0.14'
            }
            because 'Fixes CVE-2023-26048'
        }
    }
}

test {
    useJUnitPlatform()
}

sourceSets {
    integrationTest {
        java {
            compileClasspath += main.output + test.output
            runtimeClasspath += main.output + test.output
            srcDir file('src/integrationTest/java')
        }
        resources.srcDir file('src/integrationTest/resources')
    }
}

configurations {
    integrationTestImplementation.extendsFrom testImplementation
    integrationTestRuntime.extendsFrom testRuntime
}

task integrationTest(type: Test) {
    group = 'verification'
    testClassesDirs = sourceSets.integrationTest.output.classesDirs

    useJUnitPlatform()

    classpath = sourceSets.integrationTest.runtimeClasspath
    systemProperty 'tests.s3source.bucket', System.getProperty('tests.s3source.bucket')
    systemProperty 'tests.s3source.region', System.getProperty('tests.s3source.region')
    systemProperty 'tests.s3source.queue.url', System.getProperty('tests.s3source.queue.url')

    filter {
        includeTestsMatching '*IT'
    }
}

