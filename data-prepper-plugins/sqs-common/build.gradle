/*
 * Copyright OpenSearch Contributors
 * SPDX-License-Identifier: Apache-2.0
 */

plugins {
    id 'java'
}

sourceSets {
    integrationTest {
        java {
            compileClasspath += sourceSets.main.output
            runtimeClasspath += sourceSets.main.output
        }
    }
}

configurations {
    integrationTestImplementation.extendsFrom implementation
    integrationTestRuntimeOnly.extendsFrom runtimeOnly
}

dependencies {
    implementation project(':data-prepper-api')
    implementation project(':data-prepper-plugins:buffer-common')
    implementation project(':data-prepper-plugins:common')
    implementation libs.armeria.core
    implementation project(':data-prepper-plugins:aws-plugin-api')
    implementation 'software.amazon.awssdk:sqs'
    implementation 'software.amazon.awssdk:arns'
    implementation 'software.amazon.awssdk:sts'
    implementation 'io.micrometer:micrometer-core'
    implementation 'com.fasterxml.jackson.core:jackson-annotations'
    implementation 'com.fasterxml.jackson.datatype:jackson-datatype-jsr310'
    implementation 'org.hibernate.validator:hibernate-validator:8.0.1.Final'
    testImplementation 'com.fasterxml.jackson.dataformat:jackson-dataformat-yaml'
    testImplementation project(':data-prepper-plugins:blocking-buffer')
    integrationTestImplementation project(':data-prepper-core')
    integrationTestImplementation 'io.micrometer:micrometer-core'
    integrationTestImplementation 'org.junit.jupiter:junit-jupiter-api'
    integrationTestRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine'
}

test {
    useJUnitPlatform()
}

task integrationTest(type: Test) {
    testClassesDirs = sourceSets.integrationTest.output.classesDirs
    classpath = sourceSets.integrationTest.runtimeClasspath
    useJUnitPlatform()
}
