/*
 * Copyright OpenSearch Contributors
 * SPDX-License-Identifier: Apache-2.0
 */

sourceSets {
    integrationTest {
        java {
            compileClasspath += main.output + test.output
            runtimeClasspath += main.output + test.output
            srcDir file('src/integrationTest/java')
        }
    }
}

configurations {
    integrationTestImplementation.extendsFrom testImplementation
    integrationTestRuntime.extendsFrom testRuntime
}

dependencies {
    implementation project(':data-prepper-plugins:aws-plugin-api')
    implementation project(':data-prepper-api')
    implementation project(':data-prepper-plugins:common')
    implementation project(':data-prepper-plugins:failures-common')
    implementation project(':data-prepper-plugins:sqs-common')
    implementation project(':data-prepper-plugins:parse-json-processor:')
    implementation 'io.micrometer:micrometer-core'
    implementation 'com.fasterxml.jackson.core:jackson-core'
    implementation 'com.fasterxml.jackson.core:jackson-databind'
    implementation 'com.fasterxml.jackson.dataformat:jackson-dataformat-yaml'
    implementation 'software.amazon.awssdk:sqs'
    implementation 'software.amazon.awssdk:arns'
    implementation 'software.amazon.awssdk:s3'
    implementation 'software.amazon.awssdk:sts'
    implementation libs.commons.lang3
    implementation libs.armeria.core
    implementation 'org.projectlombok:lombok:1.18.26'
    implementation 'org.hibernate.validator:hibernate-validator:8.0.0.Final'
    testImplementation project(':data-prepper-test-common')
    annotationProcessor 'org.projectlombok:lombok:1.18.24'
}

jacocoTestCoverageVerification {
    dependsOn jacocoTestReport
    violationRules {
        rule {
            limit {
                minimum = 0.99
            }
        }
    }
}

task integrationTest(type: Test) {
    group = 'verification'
    testClassesDirs = sourceSets.integrationTest.output.classesDirs

    useJUnitPlatform()

    classpath = sourceSets.integrationTest.runtimeClasspath
    systemProperty 'tests.sqs.queue_url', System.getProperty('tests.sqs.queue_url')
    systemProperty 'tests.s3.bucket', System.getProperty('tests.s3.bucket')
    systemProperty 'tests.aws.region', System.getProperty('tests.aws.region')
    systemProperty 'tests.aws.role', System.getProperty('tests.aws.role')
    filter {
        includeTestsMatching '*IT'
    }
}

