/*
 * Copyright OpenSearch Contributors
 * SPDX-License-Identifier: Apache-2.0
 */

plugins {
    id 'java'
    id 'io.gatling.gradle' version '3.10.4'
}

configurations.all {
    exclude group: 'io.pebbletemplates', module: 'pebble'
}

group 'org.opensearch.dataprepper.test.performance'

dependencies {
    gatlingImplementation 'software.amazon.awssdk:auth:2.26.12'
    implementation 'com.fasterxml.jackson.core:jackson-core'
    testRuntimeOnly testLibs.junit.engine

    constraints {
        gatling('ch.qos.logback:logback-classic') {
            version {
                require '1.4.14'
            }
            because 'Fixes CVE-2023-6378'
        }
        gatling('ch.qos.logback:logback-core') {
            version {
                require '1.4.14'
            }
            because 'Keeps the version synced with logback-classic.'
        }
        zinc('org.scala-sbt:io_2.13') {
            version {
                require '1.9.7'
            }
            because 'Fixes CVE-2023-46122'
        }
        zinc('org.jline:jline') {
            version {
                require '3.25.0'
            }
            because 'CVE-2023-50572'
        }
    }
}

test {
    useJUnitPlatform()
}